; This file is part of the model: LogoClim: WorldClim 2.1 on NetLogo.
; Extensions used: `pathdir`, `sr`, and `string`.

to-report normalize-year [#year]
  assert-string-or-integer #year

  let out #year
  let pattern "^[0-9]{4}$"

  if (str-detect #year pattern = true) [
    set out read-from-string str-extract #year pattern
  ]

  (ifelse
    (
      data-series = "Historical climate data" and
      (out < 1970 or out > 2000)
     ) [
      set out 1970
    ] (
      data-series = "Historical monthly weather data" and
      (out < 1960 or out > 2021)
    ) [
      set out 1960
    ] (data-series = "Future climate data" and out < 2021) [
      set out 2021
    ]
  )

  report out
end

to-report make-list [#x]
  ifelse (not is-list? #x) [
    report (list #x)
  ] [
    report #x
  ]
end

to-report quartile [#stat]
  assert-string-or-integer #stat

  let data sort ([value] of patches with [value >= -99999])
  let length-data length data
  let quartile-length length-data / 4

  (ifelse
    (#stat = 0)[
      report first data
    ] (#stat = 1) [
      report last (sublist data 0 (quartile-length * 1))
    ] (#stat = 2) [
      report last (sublist data 0 (quartile-length * 2))
    ] (#stat = 3) [
      report last (sublist data 0 (quartile-length * 3))
    ] (#stat = 4) [
      report last data
    ] (#stat = "iqr") [
      let quartile-1 last (sublist data 0 (quartile-length * 1))
      let quartile-3 last (sublist data 0 (quartile-length * 3))

      report quartile-3 - quartile-1
    ] (#stat = "length") [
      report quartile-length
    ] [
      user-message (word "#stat not found.")
    ]
  )
end
