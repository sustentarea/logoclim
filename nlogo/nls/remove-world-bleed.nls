; __include [
;   "check-integer.nls"
;   "check-between.nls"
;   "check-list.nls"
;   "check-numeric.nls"
;   "check-world-bleed.nls"
; ]

to remove-world-bleed [#size #mode]
  assert-numeric #size
  assert-between #size 0 1
  assert-integer #mode
  assert-choice #mode [0 1]

  let test check-world-bleed #size
  let xcor-world-bleed item 0 test
  let ycor-world-bleed item 1 test

 (ifelse (#mode = 0) [
    resize-world 
      match-pcor min-pxcor xcor-world-bleed
      match-pcor max-pxcor xcor-world-bleed
      match-pcor min-pycor ycor-world-bleed
      match-pcor max-pycor ycor-world-bleed
  ] (#mode = 1) [
    let replacement nan

    ask patches with [
      (member? pxcor xcor-world-bleed) or
      (member? pycor ycor-world-bleed)
    ] [
      set value replacement
    ]
  ] [
    user-message (word
      "Mode "
      single-quote #mode
      " is not a valid option. "
      "Please check your settings."
    )
  ])
end

to-report match-pcor [#x #list]
  assert-numeric #x
  assert-list #list
  
  ifelse (empty? #list) [
    report #x
  ] [
    ifelse (#x < 0) [
      foreach (#list) [
        i -> set #x ifelse-value (#x = i) [#x + 1] [#x]
      ]
    ] [
      foreach (#list) [
        i -> set #x ifelse-value (#x = i) [#x - 1] [#x]
      ]
    ]
    
    report #x
  ]
end
